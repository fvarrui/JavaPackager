buildscript {
	repositories {
		mavenLocal()
        mavenCentral()
	}
	dependencies {
		classpath 'io.github.fvarrui:javapackager:latest.release'
	}
}

plugins {
    id 'java'
    id 'maven-publish'
	id 'com.github.ben-manes.versions' version '0.42.0'
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

dependencies {
    implementation 'commons-io:commons-io:2.6'
}

import io.github.fvarrui.javapackager.PackageTask;
import io.github.fvarrui.javapackager.model.*;

group = 'io.github.fvarrui'
version = '1.0.0'
description = 'HelloWorld for Gradle'
sourceCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'

javapackager {
	mainClass = 'io.github.fvarrui.helloworld.Main'
	additionalResources = [ file('src/main/resources/info.txt') ]
	bundleJre = true
	generateInstaller = true
    vmArgs = ["-Djava.library.path=."]
    administratorRequired = false
}

task packageForWindows(type: PackageTask, dependsOn: build) {
	description = 'Packages the application as a native Windows executable and bundles it in a zipball'
	platform = 'windows'
	winConfig {
		exeCreationTool = 'winrun4j'
		vmArgs = ["-Dprism.maxvram=512m -XX:+UseG1GC"]
		generateSetup = false
		generateMsi = false
		registry = new Registry([
			entries: [
				new RegistryEntry([
					key: "HKCU:MyGradleApp",		
					valueName: "greeting",		
					valueType: ValueType.REG_SZ,		
					valueData: "hello"
				])
			]
		])
        icoFile = file("windows/HelloWorldGradle.ico")
        setupMode = SetupMode.askTheUser
        removeOldLibs = true
        disableDirPage = false
        disableFinishedPage = false
        disableWelcomePage = false
        createDesktopIconTask = false
    }
}

task packageForLinux(type: PackageTask, dependsOn: build) {
	description = 'Packages the application as a native GNU/Linux executable and bundles it in a tarball'
	platform = 'linux'
	bundleJre = true
	createTarball = true
	scripts {
		bootstrap = file('assets/bootstrap.sh')
	}
}

task packageForMac(type: PackageTask, dependsOn: build) {
	description = 'Packages the application as a native Mac OS app and bundles it in a tarball'
	platform = 'mac'
	createTarball = true
	scripts {
		bootstrap = file('assets/bootstrap.sh')
	}
	macConfig {
		infoPlist.additionalEntries = '''
			<key>LSUIElement</key>
			<true/>
		'''
	}
}

task packageForAllPlatforms(dependsOn: [ packageForWindows, packageForMac, packageForLinux ]) {
	description = 'Packages the application for all platforms'
	group = 'JavaPackager'
}

